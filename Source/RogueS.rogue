module RogueScript

uses Console/CommandLineParser

$include RogueScript
$include RogueScript/NativeLibraries

RogueS( System.command_line_arguments )

class RogueS
  PROPERTIES
    command  : Variant

  METHODS
    method init( args:String[] )
      command = _parse_args( args )

      # trace command
      # has //options and possibly //args

      if (command//options//help or command//args.count == 0)
        _print_usage
        System.exit 0
      endIf

      try
        local vm = VM
        NativeLibraries.register( vm )
        vm.include( "Source/Libraries" )

        forEach (arg in command//args)
          vm.include( File(arg->String) )
        endForEach

        vm.update

      catch (error:RogueScriptError)
        Global.error.println error
        System.exit 1

      catch (error:Exception)
        Global.error.print   "[INTERNAL ERROR] "
        Global.error.println error
        System.exit 1

      endTry

    method _parse_args( args:String[] )->Variant
      # This method is unrelated to the Froley Parser
      local command = CommandLineParser().
      [
        option( "--flag",     &alias="-f" )
        option( "--help",     &aliases=["-h","-?"] )
        option( "--setting=", &alias="-s" )
      ].parse( args )
      return command

    method _print_usage
      println @|USAGE
               |  rogueScript [OPTIONS] <filepath>
               |
               |OPTIONS
               |  --help, -h, -?
               |    Show this help text.
endClass
